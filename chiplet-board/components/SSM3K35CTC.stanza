#use-added-syntax(jitx)
defpackage SSM3K35CTC :
  import core
  import math
  import collections
  import jitx
  import jitx/commands
  import ocdb/defaults
  import ocdb/generic-components
  import ocdb/bundles
  import ocdb/land-patterns
  import ocdb/design-vars
  import ocdb/generator-utils
  import ocdb/checks
  import ocdb/box-symbol
  import ocdb/symbols
  import utils/time


public pcb-landpattern CST3C-lp :
  pad p[1] : smd-pad(0.15,0.2) at loc(-0.175,-0.25)
  pad p[2] : smd-pad(0.15,0.2) at loc( 0.175,-0.25)
  pad p[3] : smd-pad(0.15,0.2) at loc( 0.0  , 0.25)

  layer(Courtyard(Top)) = Rectangle(0.6, 0.8)
  layer(Silkscreen("F-SilkS", Top)) = Line(0.25, [Point(-0.175, -0.3), Point( 0.175, -0.3)])
  ref-label()

public pcb-component component :
  description = "MOSFET N-CH 20V 0.18A"
  manufacturer = "Toshiba"
  mpn = "SSM3K35CTC"
  reference-prefix = "Q"
  pin-properties :
    [ pin:Ref | pads:Int ...   | Side:Dir ]
    [ g       | 1              | Left     ]
    [ s       | 2              | Down     ]
    [ d       | 3              | Up       ]
    
  val sym = fet-sym(FETNmos, FETEnhancement, true, false)
  symbol = sym(self.d => sym.d, self.s => sym.s, self.g => sym.g)

  assign-landpattern(CST3C-lp)
